#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# name: 施超 ID: 518021910499
# Describe how and why you modified the baseline code.
# 1. use iaddq to replace irmovq+addq
# 2. apply 8*1 loop unrolling to reduce loop overhead and avoid load/use hazard
# 3. implement a search table to speed up the process of copying the last 1~7
#    elements
# 4. read the last element in advance to avoid load/use hazard in the final part
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	# xorq %rax,%rax	# count = 0;
	iaddq $-7, %rdx		# len -= 8
	jl Last				# handles <8 elements

Loop:
	mrmovq (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9
	mrmovq 16(%rdi), %r10
	mrmovq 24(%rdi), %r11
	mrmovq 32(%rdi), %r12
	mrmovq 40(%rdi), %r13
	mrmovq 48(%rdi), %r14
	# mrmovq 56(%rdi), %rcx
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)
	rmmovq %r10, 16(%rsi)
	rmmovq %r11, 24(%rsi)
	rmmovq %r12, 32(%rsi)
	rmmovq %r13, 40(%rsi)
	rmmovq %r14, 48(%rsi)
	# rmmovq %rcx, 56(%rsi)
	andq %r8, %r8		# val <= 0?
	jle N1				# if so, goto Npos:
	iaddq $1, %rax		# count++
N1:	andq %r9, %r9		# val <= 0?
	jle N2				# if so, goto Npos:
	iaddq $1, %rax		# count++
N2:	andq %r10, %r10		# val <= 0?
	jle N3				# if so, goto Npos:
	iaddq $1, %rax		# count++
N3:	andq %r11, %r11		# val <= 0?
	jle N4				# if so, goto Npos:
	iaddq $1, %rax		# count++
N4:	andq %r12, %r12		# val <= 0?
	jle N5				# if so, goto Npos:
	iaddq $1, %rax		# count++
N5:	andq %r13, %r13		# val <= 0?
	jle N6				# if so, goto Npos:
	iaddq $1, %rax		# count++
N6:	andq %r14, %r14		# val <= 0?
	jle Npos			# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos:
	iaddq $56, %rdi		# src += 8
	iaddq $56, %rsi		# dst += 8
	iaddq $-7, %rdx		# len -= 8
	jge Loop			# if length >= 8, loop

# handling rest part
Last:
	mrmovq (%rdi), %r8
	iaddq $4, %rdx
	jl R02
	rmmovq %r8, (%rsi)
	jg GE4
	je R3
R02:iaddq $2, %rdx
	jl Done
	rmmovq %r8, (%rsi)
	jg R2
	je R1
GE4:iaddq $-2, %rdx
	je R5 
	jl R4

# R7:
# 	andq %r8, %r8
# 	mrmovq 48(%rdi), %r8
# 	jle A7
# 	iaddq $1, %rax
# A7:	rmmovq %r8, 48(%rsi)
R6:
	andq %r8, %r8
	mrmovq 40(%rdi), %r8
	jle A6
	iaddq $1, %rax
A6:	rmmovq %r8, 40(%rsi)
R5:
	andq %r8, %r8
	mrmovq 32(%rdi), %r8
	jle A5
	iaddq $1, %rax
A5:	rmmovq %r8, 32(%rsi)
R4:
	andq %r8, %r8
	mrmovq 24(%rdi), %r8
	jle A4
	iaddq $1, %rax
A4:	rmmovq %r8, 24(%rsi)
R3:
	andq %r8, %r8
	mrmovq 16(%rdi), %r8
	jle A3
	iaddq $1, %rax
A3:	rmmovq %r8, 16(%rsi)
R2:
	andq %r8, %r8
	mrmovq 8(%rdi), %r8
	jle A2
	iaddq $1, %rax
A2:	rmmovq %r8, 8(%rsi)
R1:	
	andq %r8, %r8
	jle Done
	iaddq $1, %rax
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
