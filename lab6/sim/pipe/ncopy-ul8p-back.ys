#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# name: 施超 ID: 518021910499
# Describe how and why you modified the baseline code.
# 1. use iaddq to replace irmovq+addq
# 2. move len-- after reading val from src to eliminate one bubble -> 11.7
# loop unrolling 8.65 -> 8.34, 8.26 del andq
# ul 2: 8.91 5: 8.23
# remove xorq %rax -> 8.19
# try union mrmovq in remainder -> 8.05
# 7.82->53.6
# 7.73
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	# xorq %rax,%rax		# count = 0;
	iaddq $-8, %rdx		# len -= 4
	jl Last			# if so, goto Done:

Loop:
	mrmovq (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9
	mrmovq 16(%rdi), %r10
	mrmovq 24(%rdi), %r11
	mrmovq 32(%rdi), %r12
	mrmovq 40(%rdi), %r13
	mrmovq 48(%rdi), %r14
	mrmovq 56(%rdi), %rcx
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)
	rmmovq %r10, 16(%rsi)
	rmmovq %r11, 24(%rsi)
	rmmovq %r12, 32(%rsi)
	rmmovq %r13, 40(%rsi)
	rmmovq %r14, 48(%rsi)
	rmmovq %rcx, 56(%rsi)
	andq %r8, %r8		# val <= 0?
	jle N1			# if so, goto Npos:
	iaddq $1, %rax		# count++
N1:	andq %r9, %r9		# val <= 0?
	jle N2			# if so, goto Npos:
	iaddq $1, %rax		# count++
N2:	andq %r10, %r10		# val <= 0?
	jle N3			# if so, goto Npos:
	iaddq $1, %rax		# count++
N3:	andq %r11, %r11		# val <= 0?
	jle N4			# if so, goto Npos:
	iaddq $1, %rax		# count++
N4:	andq %r12, %r12		# val <= 0?
	jle N5			# if so, goto Npos:
	iaddq $1, %rax		# count++
N5:	andq %r13, %r13		# val <= 0?
	jle N6			# if so, goto Npos:
	iaddq $1, %rax		# count++
N6:	andq %r14, %r14		# val <= 0?
	jle N7			# if so, goto Npos:
	iaddq $1, %rax		# count++
N7:	andq %rcx, %rcx		# val <= 0?
	jle Npos			# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos:
	iaddq $64, %rdi		# src++
	iaddq $64, %rsi		# dst++
	iaddq $-8, %rdx		# len--
	jge Loop			# if so, goto Loop:

# handling rest part
Last:
	mrmovq (%rdi), %r8

# Scheme 1	56.7
# 	iaddq $4, %rdx
# 	jge G4
# L3: iaddq $3, %rdx
# 	jl Done
# 	rmmovq %r8, (%rsi)
# 	je R1
# 	jl G2
# G2: 
# 	iaddq $-1, %rdx
# 	je R2
# 	jg R3
# G4:
# 	# mrmovq (%rdi), %r8
# 	rmmovq %r8, (%rsi)
# 	je R4
# G5: iaddq $-2, %rdx
# 	jg R7
# 	je R6
# 	jmp R5

# 59.1
	iaddq $5, %rdx
	jge GE3
LE2:
	iaddq $2, %rdx
	jl Done
	rmmovq %r8, (%rsi)
	je R1
	jg R2
LE6:
	iaddq $2, %rdx
	jl R4
	je R5
	jg R6
GE3:
	rmmovq %r8, (%rsi)
	je R3
	iaddq $-4, %rdx
	jl LE6

# 	iaddq $7, %rdx
# 	jg R27
# 	je R1
# 	jl Done
# R27:iaddq $-2, %rdx
# 	jg R47
# 	je R3
# 	jl R2
# R47:iaddq $3, %rdx
# 	jg R67
# 	je R5
# 	jl R4
# R67:iaddq $-1, %rdx
# 	je R6

R7:
	andq %r8, %r8
	mrmovq 48(%rdi), %r8
	jle A7
	iaddq $1, %rax
A7:	rmmovq %r8, 48(%rsi)
R6:
	andq %r8, %r8
	mrmovq 40(%rdi), %r8
	jle A6
	iaddq $1, %rax
A6:	rmmovq %r8, 40(%rsi)
R5:
	andq %r8, %r8
	mrmovq 32(%rdi), %r8
	jle A5
	iaddq $1, %rax
A5:	rmmovq %r8, 32(%rsi)
R4:
	andq %r8, %r8
	mrmovq 24(%rdi), %r8
	jle A4
	iaddq $1, %rax
A4:	rmmovq %r8, 24(%rsi)
R3:
	andq %r8, %r8
	mrmovq 16(%rdi), %r8
	jle A3
	iaddq $1, %rax
A3:	rmmovq %r8, 16(%rsi)
R2:
	andq %r8, %r8
	mrmovq 8(%rdi), %r8
	jle A2
	iaddq $1, %rax
A2:	rmmovq %r8, 8(%rsi)
R1:	
	andq %r8, %r8
	jle Done
	iaddq $1, %rax
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
